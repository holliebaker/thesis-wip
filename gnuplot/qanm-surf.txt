#!/usr/bin/gnuplot
#
# Plotting a sphere with Gnuplots parametric mode
#
# AUTHOR: Hagen Wierstorf
# VERSION: gnuplot 4.6 patchlevel 0

#set terminal pngcairo size 800,800
#set output 'qanm-surf.png'

set terminal svg size 800,800
set output 'qanm-surf.svg'


set lmargin at screen 0.05
set rmargin at screen 0.95
set bmargin at screen 0.05
set tmargin at screen 0.95
unset title
unset key
unset border
unset xtics
unset ytics

set parametric
set isosamples 100,100

set xrange [-0.3:1.3]
set yrange [-0.2:1.3]
set zrange [-0.3:1.3]
set urange [-0.1:1.1]
set vrange [-0.1:1.1]
set size ratio -1
set view 70, 330

#set palette rgb 33,13,10
set cbrange [0:1]
set style fill transparent solid 0.65
set style line 1 lc rgb 'black' lt 1 lw 2 pt 7
set linetype 1 lc rgb "black" lw 0.1
set linetype 2 lc rgb "black" lw 0.5

f(u,v) = u**2 + v**2
c(u,v) = u >= 0 && v >= 0 && u + v <= 1

set pm3d depthorder hidden3d 1
set hidden3d front
set pm3d depthorder base
set pm3d lighting
splot \
    u,v, -0.11 with lines lt 1, \
    u,v, c(u,v) ? -0.1 : 1/0 with lines lt 2, \
    u,v, c(u,v) ? f(u,v) : 1/0 with pm3d

# projection, surface, bottom, top, lines (left, centre, right), dividing plane

set output
